apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

tasks.withType(GroovyCompile) {
    groovyOptions.encoding = "MacRoman"
}
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

group = publishedGroupId

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                groupId publishedGroupId
                artifactId artifact

                // Add your description here
                name libraryName
                description libraryDescription
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name licenseName
                        url licenseUrl
                    }
                }
                developers {
                    developer {
                        id developerId
                        name developerName
                        email developerEmail
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}

version = libraryVersion

if (project.hasProperty("android")) { // Android libraries
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        destinationDir = reporting.file("${project.projectDir}${File.separator}${libraryName}jar")
        from android.sourceSets.main.java.srcDirs
//        into "${project.projectDir}${File.separator}${libraryName}jar"
    }

    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        destinationDir = reporting.file("${project.projectDir}${File.separator}${libraryName}doc")
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }
} else { // Java libraries
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        destinationDir = reporting.file("${project.projectDir}${File.separator}${libraryName}jar")
        from sourceSets.main.allSource
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
    destinationDir = reporting.file("${project.projectDir}${File.separator}${libraryName}jar")
}
/**
 * 配置javadoc的编码格式
 */
javadoc {
    options {
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        links "http://docs.oracle.com/javase/8/docs/api"
        title artifact
    }
    println(">>>>" + project.projectDir)
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())//获取local.properties中的配置文件

bintray {
    user = properties.getProperty("bintray.user")//local.properties中的user属性用户名
    key = properties.getProperty("bintray.apikey")//local.properties中的apikey属性，这个需要保密从bintray中获得

    configurations = ['archives']
    pkg {
        repo = bintrayRepo
        name = bintrayName
        desc = libraryDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = allLicenses
        publish = true
        publicDownloadNumbers = true
        version {
            desc = libraryDescription
            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
                passphrase = properties.getProperty("bintray.gpg.password")//bintray登陆的密码
                //Optional. The passphrase for GPG signing'
            }
        }
    }
}


task upload() {}
upload.doFirst {}.dependsOn(install)
upload.dependsOn(clean)

upload.finalizedBy(bintrayUpload)